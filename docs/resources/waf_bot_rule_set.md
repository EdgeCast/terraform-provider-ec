---
page_title: "edgecast_waf_bot_rule_set Resource"
subcategory: "Web Application Firewall (WAF)"
description: |-
  edgecast_waf_bot_rule_set Resource
---

# edgecast_waf_bot_rule_set Resource

Use bot rules to require a client (e.g., a web browser) to solve a challenge before resolving the request. WAF blocks traffic when the client cannot solve this challenge within a few seconds. Basic bots typically cannot solve this type of challenge and therefore their traffic is blocked. This prevents them from scraping your site, carding, spamming your forms, launching DDoS attacks, and committing ad fraud.

-> Apply a bot rule set to your traffic by adding it to a [Security Application Manager](https://docs.edgecast.com/cdn/#Web-Security/SAM.htm) configuration.

-> You may manage an existing bot rule set by importing it as a resource.  
[Learn more.](#import-resource)

## Authentication

This resource requires a [REST API token](../guides/authentication#rest-api-token).

## Example Usage

```terraform
resource "edgecast_waf_bot_rule_set" "bot_rule_set_1" {
  account_number = "0001"
  name        = "Bot Rule Terraform Test"

  directive {
    include = "r3010_ec_bot_challenge_reputation.conf.json"
  }

  directive {
    sec_rule {
      name = "Sec Rule 2"
      action {
        id              = 77000000
        msg             = "Invalid user agent"
        transformations = ["NONE"]
      }
      operator {
        is_negated = false
        type       = "CONTAINS"
        value      = "myheadervalue"
      }
      variable {
        is_count = false
        type     = "REQUEST_HEADERS"
        match {
          is_negated = false
          is_regex   = false
          value      = "myheader"
        }
      }

      chained_rule {
        action {
          transformations = ["LOWERCASE"]
        }
        operator {
          is_negated = false
          type       = "BEGINSWITH"
          value      = "bot"
        }
        variable {
          is_count = false
          type     = "REQUEST_HEADERS"
          match {
            is_negated = false
            is_regex   = false
            value      = "User-Agent"
          }
        }
      }
    }
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `account_number` (String) Identifies your account. Find your account number in the upper right-hand corner of the MCC.
- `directive` (Block Set, Min: 1) Contains the bot rules associated with this bot rule set. 

    ->You may create up to 10 bot rules per bot rule set. (see [below for nested schema](#nestedblock--directive))

### Optional

- `name` (String) Indicates the name of the bot rule set.

### Read-Only

- `id` (String) Indicates the system-defined ID for this Bot Rule Set.
- `last_modified_date` (String) Indicates the date and time at which the bot rule set was last modified.

<a id="nestedblock--directive"></a>
### Nested Schema for `directive`

Optional:

- `include` (String) Identifies a bot rule that uses our reputation database. This type of rule is satisfied when the client's IP address matches an IP address defined within our reputation database. Our reputation database contains a list of IP addresses known to be used by bots.  
Set this argument to the following value to include a bot rule that uses our reputation database: 

        r3010_ec_bot_challenge_reputation.conf.json
- `sec_rule` (Block Set, Max: 1) Identifies a bot rule that uses custom match conditions. This type of rule is satisfied when a match is found for each of its conditions. A condition determines request identification by defining what will be matched (i.e., variable), how it will be matched (i.e., operator), and a match value. (see [below for nested schema](#nestedblock--directive--sec_rule))

<a id="nestedblock--directive--sec_rule"></a>
### Nested Schema for `directive.sec_rule`

Required:

- `action` (Block Set, Min: 1) Determines whether the string identified in a `variable` block will be transformed and defines metadata through which you may identify traffic to which this bot rule was applied. (see [below for nested schema](#nestedblock--directive--sec_rule--action))
- `operator` (Block Set, Min: 1, Max: 1) Indicates the comparison that will be performed against the request element(s) identified within a `variable` block. (see [below for nested schema](#nestedblock--directive--sec_rule--operator))
- `variable` (Block List, Min: 1) Contains criteria that identifies a request element. (see [below for nested schema](#nestedblock--directive--sec_rule--variable))

Optional:

- `chained_rule` (Block List) Contains additional criteria that must be satisfied to identify traffic to which this bot  rule will be applied. 

    ->You may add up to 5 `chained_rule` blocks per bot rule. (see [below for nested schema](#nestedblock--directive--sec_rule--chained_rule))
- `name` (String) Indicates the name assigned to this bot rule.

<a id="nestedblock--directive--sec_rule--action"></a>
### Nested Schema for `directive.sec_rule.action`

Optional:

- `id` (String) Determines the custom ID that will be assigned to this rule set. This custom ID is exposed via the Browser Challenges Dashboard. Valid values fall within this range: `77000000 - 77999999`.  
**Default Value:** Random number 

    ->This field is only applicable for the `action` block that resides in the root of the `sec_rule` block.
- `msg` (String) Determines the rule message that will be assigned to this rule. This message is exposed via the Browser Challenges Dashboard.  
**Default Value:** Blank
- `transformations` (List of String) Determines the set of transformations that will be applied to the value  derived from the request element identified in a `variable` block (i.e., source value). Transformations are always applied to the source value, regardless of the number of transformations that have been defined.	Valid values are: 
 * `NONE` - Indicates that the source value should not be modified. 
 * `LOWERCASE` - Indicates that the source value should be converted to lowercase characters. 
 * `REMOVENULLS` - Indicates that null values should be removed from the source value. 

    ->A criterion is satisfied if the source value or any of the modified string values meet the conditions defined by the `operator` block.


<a id="nestedblock--directive--sec_rule--operator"></a>
### Nested Schema for `directive.sec_rule.operator`

Required:

- `type` (String) Indicates how the system will interpret the comparison between the `value` argument and the value derived from the request element defined within a `variable` block. Valid values are: 
 * `RX` - Indicates that the string value derived from the request element must satisfy the regular expression defined in the `value` argument. 
 * `STREQ` - Indicates that the string value derived from the request element must be an exact match to the `value` argument. 
 * `CONTAINS` - Indicates that the `value` argument must contain the string value derived from the request element. 
 * `BEGINSWITH` - Indicates that the `value` argument must start with the string value derived from the request element. 
 * `ENDSWITH` - Indicates that the `value` argument must end with the string value	derived from the request element. 
 * `EQ` - Indicates that the number derived from the `variable` block must be an exact match to the `value` argument. 

        ->You should only use `EQ` when the `is_count` argument has been enabled. 
 * `IPMATCH` - Requires that the request's IP address either be contained by an IP block or be an exact match to an IP address defined in the `values` argument. Only use `IPMATCH` with the `REMOTE_ADDR` variable.

Optional:

- `is_negated` (Boolean) Indicates whether a condition will be satisfied when the value derived from the request element defined within a `variable` block matches or does not match the `value` argument. Valid values are: 
 * `True` - Does not match 
 * `False` - Matches
- `value` (String) Indicates a value that will be compared against the string or number value derived from the request element defined within a `variable` block.


<a id="nestedblock--directive--sec_rule--variable"></a>
### Nested Schema for `directive.sec_rule.variable`

Required:

- `type` (String) Determines the request element that will be assessed.	Valid values are: 

        GEO | QUERY_STRING | REMOTE_ADDR | REMOTE_ASN | REQUEST_COOKIES | REQUEST_FILENAME | REQUEST_HEADERS | REQUEST_METHOD | REQUEST_URI 

    ->If a request element consists of one or more key-value pairs, then  you may identify a key via a `match` block. If `is_count` has been disabled, then you may identify a specific value via the `operator` block.

Optional:

- `is_count` (Boolean) Determines whether a comparison will be performed between the `operator` block and a string value or the number of matches found. Valid values are: 
 * `True` - A counter will increment whenever the request element defined by this `variable` block is found. The `operator` block will perform a comparison against this number. 

    ->If you enable `is_count`, then you must also set the `type` argument to `EQ`. 
 * `False` - The `operator` block will perform a comparison against the string value derived from the request element defined by this `variable` block.
- `match` (Block List) Contains comparison settings for the request element identified by the `type` argument. (see [below for nested schema](#nestedblock--directive--sec_rule--variable--match))

<a id="nestedblock--directive--sec_rule--variable--match"></a>
### Nested Schema for `directive.sec_rule.variable.match`

Optional:

- `is_negated` (Boolean) Determines whether this condition is satisfied when the request element identified by the `variable` block is found or not found. Valid values are: 
 * `True` - Not found.  
 * `False` - Found
- `is_regex` (Boolean) Determines whether the `value` argument will be interpreted as a regular expression. Valid values are: 
 * `True` - Regular expression 
 * `False` - Default value. Literal value.
- `value` (String) Restricts the match condition defined by the `type` argument to the specified value.  
  
**Example:**  
If the `type` argument is set to `REQUEST_HEADERS` and this argument is set to `User-Agent`, then this match condition is restricted to the `User-Agent` request header. If the `value` argument is omitted, then this match condition applies to all request headers.



<a id="nestedblock--directive--sec_rule--chained_rule"></a>
### Nested Schema for `directive.sec_rule.chained_rule`

Required:

- `action` (Block Set, Min: 1) Determines whether the string value derived from the request element identified in a `variable` block will be transformed and metadata through which you may identify traffic to which this bot rule was applied. (see [below for nested schema](#nestedblock--directive--sec_rule--chained_rule--action))
- `operator` (Block Set, Min: 1, Max: 1) Indicates the comparison that will be performed against the request element(s) identified within a `variable` block. (see [below for nested schema](#nestedblock--directive--sec_rule--chained_rule--operator))

Optional:

- `variable` (Block List) Identifies each request element for which a comparison will be made. (see [below for nested schema](#nestedblock--directive--sec_rule--chained_rule--variable))

<a id="nestedblock--directive--sec_rule--chained_rule--action"></a>
### Nested Schema for `directive.sec_rule.chained_rule.variable`

Optional:

- `transformations` (List of String) Determines the set of transformations that will be applied to the value derived from the request element identified in a `variable` block (i.e., source value). Transformations are always applied to the source value, regardless of the number of transformations that have been defined. Valid values are: 
 * `NONE` - Indicates that the source value should not be modified. 
 * `LOWERCASE` - Indicates that the source value should be converted to lowercase characters. 
 * `REMOVENULLS` - Indicates that null values should be removed from the source value. 

    ->A criterion is satisfied if the source value or any of the modified string values meet the conditions defined by the `operator` block.


<a id="nestedblock--directive--sec_rule--chained_rule--operator"></a>
### Nested Schema for `directive.sec_rule.chained_rule.variable`

Required:

- `type` (String) Indicates how the system will interpret the comparison between the `value` argument and the value derived from the request element defined within a `variable` block. Valid values are: 
 * `RX` - Indicates that the string value derived from the request element must satisfy the regular expression defined in the `value` argument. 
 * `STREQ` - Indicates that the string value derived from the request element must be an exact match to the `value` argument. 
 * `CONTAINS` - Indicates that the `value` argument must contain the string value derived from the request element. 
 * `BEGINSWITH` - Indicates that the `value` argument must start with the string value derived from the request element. 
 * `ENDSWITH` - Indicates that the `value` argument must end with the string value	derived from the request element. 
 * `EQ` - Indicates that the number derived from the `variable` block must be an exact match to the `value` argument. 

        ->You should only use `EQ` when the `is_count` argument has been enabled. 
 * `IPMATCH` - Requires that the request's IP address either be contained by an IP block or be an exact match to an IP address defined in the `values` argument. Only use `IPMATCH` with the `REMOTE_ADDR` variable.

Optional:

- `is_negated` (Boolean) Indicates whether a condition will be satisfied when the value derived from the request element defined within a `variable` block matches or does not match the `value` argument. Valid values are: 
 * `True` - Does not match 
 * `False` - Matches
- `value` (String) Indicates a value that will be compared against the string or number value derived from the request element defined within a `variable` block.


<a id="nestedblock--directive--sec_rule--chained_rule--variable"></a>
### Nested Schema for `directive.sec_rule.chained_rule.variable`

Required:

- `type` (String) Determines the request element that will be assessed.	Valid values are: 

        GEO | QUERY_STRING | REMOTE_ADDR | REMOTE_ASN | REQUEST_COOKIES | REQUEST_FILENAME | REQUEST_HEADERS | REQUEST_METHOD | REQUEST_URI 

    ->If a request element consists of one or more key-value pairs, then  you may identify a key via a `match` block. If `is_count` has been disabled, then you may identify a specific value via the `operator` block.

Optional:

- `is_count` (Boolean) Determines whether a comparison will be performed between the `operator` block and a string value or the number of matches found. Valid values are: 
 * `True`  - A counter will increment whenever the request element defined by this `variable` block is found. The `operator` block will perform a comparison against this number. 

        ->If you enable `is_count`, then you must also set the `type` argument to `EQ`.
 * `False` - The `operator` block will perform a comparison against the string value derived from the request element defined by this `variable` block.
- `match` (Block List) Determines the comparison conditions for the request element identified by the `type` argument. (see [below for nested schema](#nestedblock--directive--sec_rule--chained_rule--variable--match))

<a id="nestedblock--directive--sec_rule--chained_rule--variable--match"></a>
### Nested Schema for `directive.sec_rule.chained_rule.variable.match`

Optional:

- `is_negated` (Boolean) Determines whether this condition is satisfied when the request element identified by the `variable` block is found or not found. Valid values are: 
 * `True` - Not found. If this argument has been enabled, then the `match` block should contain an initial block that sets both the `is_negated` and `is_regex` properties to `False`. 
 * `False` - Found
- `is_regex` (Boolean) Determines whether the `value` argument will be interpreted as a regular expression. Valid values are: 
 * `True` - Regular expression 
 * `False` - Default value. Literal value.
- `value` (String) Restricts the match condition defined by the `type` argument to the specified value.  
  
**Example:**  
If the `type` argument is set to `REQUEST_HEADERS` and this argument is set to	`User-Agent`, then this match condition is restricted to the `User-Agent` request header. If the `value` argument is omitted, then this match condition	applies to all request headers.


## Import Resource
Manage an existing bot rule set through Terraform by importing it as a resource. Perform the following steps:
1. Insert an empty resource block within your resource configuration.

        resource "edgecast_waf_bot_rule_set" "<RESOURCE>" {
          
        }
    **Example:**

        resource "edgecast_waf_bot_rule_set" "sample_bot_rule_set" {
          
        }
1. Run the following command to attach a bot rule set to your resource configuration.

        terraform import edgecast_waf_bot_rule_set.<RESOURCE> <ACCOUNT_NUMBER>:<BOT_RULE_SET_ID>
    * `<RESOURCE>` - Replace this term with the name of the resource defined in step 1.
    * `<ACCOUNT_NUMBER>` - Replace this term with your customer account number. Find your account number in the upper right-hand corner of the MCC.
    * `<BOT_RULE_SET_ID>` - Replace this term with the system-defined ID assigned to the desired bot rule set. You may retrieve a list of bot rule sets and their system-defined IDs through our [REST API](https://developer.edgecast.com/cdn/api/index.html#Media_Management/Web-Security/Get-All-Bot-Rule-Sets.htm).

    **Example:**

        terraform import edgecast_waf_bot_rule_set.sample_bot_rule_set 0001:123456
->Upon running the above command, a resource for that bot rule set will be recorded in the state file.
